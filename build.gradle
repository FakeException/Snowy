plugins {
    id 'java'
    id 'application'
    id "org.openjfx.javafxplugin" version "0.0.10"
    id "io.freefair.lombok" version "6.0.0-m2"
    id 'org.beryx.runtime' version '1.12.5'
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.zaxxer:HikariCP:4.0.3'
    implementation 'io.github.palexdev:materialfx:11.12.0'
    implementation 'mysql:mysql-connector-java:8.0.25'
    implementation 'com.sun.mail:javax.mail:1.6.2'
    implementation("com.squareup.okhttp3:okhttp:4.9.0")
}

javafx {
    version = "16"
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.graphics', 'javafx.base', 'javafx.media']
}

mainClassName = "me.darkboy.Launcher"

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    launcher {
        noConsole = true
    }

    jpackage {
        // Uncomment and adjust the following line if your runtime task is configured to generate images for multiple platforms
        // targetPlatformName = "mac"

        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        def imgType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'
        imageOptions += ['--icon', "src/main/resources/me/darkboy/images/logo290.$imgType"]
        installerOptions += ['--resource-dir', "src/main/resources"]
        installerOptions += ['--vendor', 'FakeException']

        if(currentOs.windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
        }
        else if (currentOs.linux) {
            installerOptions += ['--linux-package-name', 'snowy','--linux-shortcut']
        }
        else if (currentOs.macOsX) {
            installerOptions += ['--mac-package-name', 'snowy']
        }
    }
}